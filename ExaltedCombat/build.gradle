apply plugin: 'java';

String mainClass = 'exaltedcombat.TickCombatFrame';

sourceCompatibility = '1.7';

def compilerOptions = [compileJava, compileTestJava]*.options;
compilerOptions*.encoding = 'UTF-8';
compilerOptions*.compilerArgs = ['-Xlint'];

repositories {
    mavenCentral();
    maven {
        url 'http://dl.bintray.com/kelemen/maven';
    }
}

task run(type: JavaExec, dependsOn: compileJava) {
    main = mainClass;
    classpath = sourceSets.main.runtimeClasspath;
}

task debug(type: JavaExec, dependsOn: compileJava) {
    main = mainClass;
    classpath = sourceSets.main.runtimeClasspath;
    debug = true;
}

File packageDir = buildDir.toPath().resolve('package').toFile();

task copyDependencies(type: Copy) {
    from configurations.runtime;
    into packageDir.toPath().resolve('lib').toFile();
}

task ('package', type: Copy, dependsOn: [copyDependencies, jar]) {
    from jar.archivePath;
    into packageDir;
}

jar.doFirst {
    manifest {
        def manifestClasspath = configurations.runtime.collect { 'lib/' + it.getName() }.join(' ');
        attributes 'Class-Path': manifestClasspath;
        attributes 'Main-Class': mainClass;
    }
}

dependencies {
    compile group: 'org.jtrim', name: 'jtrim-gui', version: '1.6.0';
    testCompile group: 'junit', name: 'junit', version: '4.10';
}